apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.devtools.ksp'

def noKeyFound = '"NO_KEY_FOUND"'
def googleMapsAPIKey = noKeyFound
def thisAppLicensingKey = noKeyFound

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
    if (localProperties['googleMapsAPIKey']) {
        googleMapsAPIKey = localProperties['googleMapsAPIKey']
    }
    if (localProperties['thisAppLicensingKey']) {
        thisAppLicensingKey = localProperties['thisAppLicensingKey']
    }
}

if (googleMapsAPIKey == noKeyFound) {
    googleMapsAPIKey = '"YOUR_API_KEY_GOES_HERE"'
}

android {

    compileSdk 35

    defaultConfig {
        applicationId "app.simple.positional"
        minSdkVersion 24
        //noinspection OldTargetApi
        targetSdkVersion 35
        versionCode 20250706
        versionName "v2025.07.06"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        resValue "string", "googleMapsAPIKey", googleMapsAPIKey
        resValue "string", "thisAppLicensingKey", thisAppLicensingKey

        ksp {
            arg("room.schemaLocation", "$projectDir/databases/schemas")
        }
    }

    bundle {
        language {
            enableSplit = false
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "_debug"
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    namespace 'app.simple.positional'

    buildFeatures {
        aidl true
        buildConfig true
        viewBinding false
    }

    applicationVariants.configureEach { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }
}

dependencies {

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'

    // Tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.fragment:fragment-ktx:1.8.8'
    //noinspection GradleDependency
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    implementation 'androidx.webkit:webkit:1.14.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.9.1'

    // Kotlin
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'

    // Google Services and Default android libs
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation 'com.google.android.gms:play-services-maps:19.2.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.maps.android:android-maps-utils:3.14.0'
    implementation 'com.google.android.play:app-update-ktx:2.1.0'

    // Third Party
    implementation 'org.shredzone.commons:commons-suncalc:3.11'
    implementation 'com.github.Berico-Technologies:Geo-Coordinate-Conversion-Java:360781e5be'
    implementation 'com.github.vipulasri:timelineview:1.2.2'

    // Room components
    //noinspection GradleDependency
    implementation "androidx.room:room-ktx:2.6.1"
    //noinspection GradleDependency,KaptUsageInsteadOfKsp
    ksp "androidx.room:room-compiler:2.6.1"
    //noinspection GradleDependency
    androidTestImplementation "androidx.room:room-testing:2.6.1"

    // Leak Canary -- debugImplementation because LeakCanary should only run in debug builds.
    // debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.5'
}